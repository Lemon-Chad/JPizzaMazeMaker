
import json;
import time;
import iofile as io;
import sys;
var {*} => import std;

class BoxChars {
    stc TOP: String => "─";
    stc LEFT: String => "│";
    stc RIGHT: String => "│";
    stc BOTTOM: String => "═";

    stc TOP_LEFT: String => "┌";
    stc TOP_RIGHT: String => "┐";
    stc BOTTOM_LEFT: String => "╘";
    stc BOTTOM_RIGHT: String => "╛";

    stc INNER_TOP_LEFT: String => "┘";
    stc INNER_TOP_RIGHT: String => "└";
    stc INNER_BOTTOM_LEFT: String => "╕";
    stc INNER_BOTTOM_RIGHT: String => "╒";
}

obj Box {
    top: bool;
    left: bool;
    right: bool;
    bottom: bool;
    visited: [bool];
    marked: [bool];

    pub x: num;
    pub y: num;

    ingredients<x #num, y #num> {
        top => false;
        left => false;
        right => false;
        bottom => false;

        visited => &false;
        marked => &false;

        attr x => x;
        attr y => y;
    }

    mthd visit = Box {
        if (!visited) {
            top => false;
            left => false;
            right => false;
            bottom => false;
        }

        visited => &true;
        this
    }

    mthd openDown = Box {
        bottom => true;
        this
    }

    mthd openUp = Box {
        top => true;
        this
    }

    mthd openLeft = Box {
        left => true;
        this
    }
    
    mthd openRight = Box {
        right => true;
        this
    }

    mthd bin string = String {
        let t => ? !top: BoxChars::TOP $_: " ";
        let l => ? !left: BoxChars::LEFT $_: " ";
        let r => ? !right: BoxChars::RIGHT $_: " ";
        let b => ? !bottom: BoxChars::BOTTOM $_: " ";

        let tl => ? (!left & !top): BoxChars::TOP_LEFT
                  $ (!left &  top): BoxChars::LEFT
                  $ ( left & !top): BoxChars::TOP
                  $_: BoxChars::INNER_TOP_LEFT;
        
        let tr => ? (!right & !top): BoxChars::TOP_RIGHT
                  $ (!right &  top): BoxChars::RIGHT
                  $ ( right & !top): BoxChars::TOP
                  $_: BoxChars::INNER_TOP_RIGHT;

        let bl => ? (!left & !bottom): BoxChars::BOTTOM_LEFT
                  $ (!left &  bottom): BoxChars::LEFT
                  $ ( left & !bottom): BoxChars::BOTTOM
                  $_: BoxChars::INNER_BOTTOM_LEFT;
        
        let br => ? (!right & !bottom): BoxChars::BOTTOM_RIGHT
                  $ (!right &  bottom): BoxChars::RIGHT
                  $ ( right & !bottom): BoxChars::BOTTOM
                  $_: BoxChars::INNER_BOTTOM_RIGHT;

        let m => ? marked: " X "
                 $_:       "   ";
        
        tl +  t  * 3 + tr + "\n" +
        l  +  m      + r  + "\n" +
        bl +  b  * 3 + br
    }

    mthd printLine<i #num> = void {
        print(split(str(this), "\n")[i]);
    }

    mthd getLine<i #num> = String {
        split(str(this), "\n")[i]
    }
}

let settings => json::loads(io::readFile("config.json"));
const WIDTH => settings["width"];
const HEIGHT => settings["height"];

let pointer => Tuple(0, 0);
let level => scope {
    let level => Array(
        ..for (y -> 0:HEIGHT) => Array(
            ..for (x -> 0:WIDTH) => Box(x, y)
        )<Box>
    )<Array(Box)>;

    for (x -> 1:--WIDTH) {
        (level[0][x])::openDown();
        (level[0][x])::openLeft();
        (level[0][x])::openRight();

        (level[-1][x])::openUp();
        (level[-1][x])::openLeft();
        (level[-1][x])::openRight();

        for (y -> 1:--HEIGHT) {
            (level[y][0])::openDown();
            (level[y][0])::openUp();
            (level[y][0])::openRight();

            (level[y][-1])::openDown();
            (level[y][-1])::openUp();
            (level[y][-1])::openLeft();

            (level[y][x])::openUp();
            (level[y][x])::openDown();
            (level[y][x])::openLeft();
            (level[y][x])::openRight();
        }
    }

    level
};

fn boxes<ptr=null> {
    if (ptr == null)
        ptr => pointer;
    
    let u => None;
    let d => None;
    let l => None;
    let r => None;

    if (*pointer[0] < --WIDTH)
        r => Some(*level[*pointer[1]][*pointer[0] + 1]);
    if (*pointer[0] > 0)
        l => Some(*level[*pointer[1]][*pointer[0] - 1]);
    if (*pointer[1] < --HEIGHT)
        d => Some(*level[*pointer[1] + 1][*pointer[0]]);
    if (*pointer[1] > 0)
        u => Some(*level[*pointer[1] - 1][*pointer[0]]);
    
    Tuple(u, d, l, r)
}

fn allVisited {
    for (row <- list(level))
        for (box <- list(row))
            if (!box::visited)
                return false;
    true
};

fn frame<string #String> = String {
    let lines => split(string, "\n");

    let s => "+" + "─" * (size(lines[0]) + 2) + "+\n";
    for (line <- lines)
        s += "│ " + line + " │\n";
    s + "+" + "─" * (size(lines[0]) + 2) + "+"
}

fn levelStr {
    let s => "";

    for (row <- list(level))
        for (i -> 0:3) {
            for (box <- list(row))
                s += box::getLine(i);
            s += "\n";
        }

    frame(s)
};

fn printLevel = void {
    for (row <- level)
        for (i -> 0:3) {
            for (box <- row)
                box::printLine(i);
            println();
        }
}

fn surrounded {
    let tup => boxes();

    for (box <- list(tup)) if (match (*box) {
        Some(val) -> !*val::visited;
        None -> false;
    }) return false;

    true
};

let stack => Array(pointer)<([num] [num])>;

let animated => settings["animate_creation"];

loop {
    if (animated) 
        println(levelStr());

    while (surrounded() & size(stack) > 0) {
        stack => stack::slice(0, stack::size() - 1);
        if (size(stack) > 0)
            pointer => *stack[-1];
    }
    
    if (allVisited()) break;

    let bxs => boxes();
    let current => (level[*pointer[1]][*pointer[0]])::visit();
    
    let possibilites => Array()<num>;
    for (x -> 0:4) if (match (*bxs[x]) {
            Some(val) -> !val::visited;
            None -> false;
        }) possibilites::add(x);
    let nxt => choose(list(possibilites));

    let nxtBxt => (bxs[*nxt])::val;
    nxtBxt::visit();
    switch (nxt) {
        case 0:
            current::openUp();
            nxtBxt::openDown();
            break;
        case 1:
            current::openDown();
            nxtBxt::openUp();
            break;
        case 2:
            current::openLeft();
            nxtBxt::openRight();
            break;
        case 3:
            current::openRight();
            nxtBxt::openLeft();
            break;
    }
    pointer => Tuple(nxtBxt::x, nxtBxt::y);
    stack::add(pointer);

    if (animated)
        time::halt(1000 / (WIDTH * HEIGHT));
}

(level[0][0])::openLeft();

let seed => randint(0, 2);
var end;
if (seed == 0)
    end => (level[randint(1, --HEIGHT)][0])::openLeft();
elif (seed == 1)
    end => (level[randint(0, --HEIGHT)][-1])::openRight();
else
    end => (level[-1][randint(1, --WIDTH)])::openDown();

println("\n" * 100);
println(levelStr());

let st => " Made with JPizza ";
let sz => round(5WIDTH / 2) - st / 2 + 2;
println("─" * sz + st + "─" * sz);

if (!settings["solve"])
    sys::exit(0);
println("Solving!");

for (row <- list(level)) for (box <- list(row)) box::visited => false;

obj Node {
    pub x: num;
    pub y: num;
    pub parent: Option;

    ingredients<x #num, y #num> {
        parent => None;

        attr x => x;
        attr y => y;
    }

    mthd setParent<p #Node> {
        parent => Some(p);
    }

    mthd bin eq<o #Node> {
        x == o::x & y == o::y
    }

    mthd bin string = String -> `(${x}, ${y})` + match (parent) { 
        Some(val) -> " -> " + val; 
        None -> "";
    };
}

end => Node(end::x, end::y);

let ptrs => Array(Node(0, 0))<Node>;
var winner;
while (!ptrs::contains(end)) {
    let next => Array()<Node>;
    for (i -> 0:ptrs::size()) {
        let ptr => *ptrs[i];
        let current => *level[ptr::y][ptr::x];

        let OFFSETMAP => {
            "top": [0, -1],
            "bottom": [0, 1],
            "left": [-1, 0],
            "right": [1, 0]
        };

        for (key <- list(OFFSETMAP)) {
            if (!getattr(current, key)) continue;
            let offset => OFFSETMAP[key];
            let nx => ptr::x + offset[0];
            let ny => ptr::y + offset[1];

            if (nx < 0 | nx >= WIDTH | ny < 0 | ny >= HEIGHT) continue;

            let box => *level[ny][nx];
            if (!box::visited) {
                box::visited => true;
                let nxt => Node(nx, ny);
                nxt::setParent(ptr);
                next::add(nxt);
                
                if (nxt == end)
                    winner => ptr;
            }
        }
    }
    ptrs => next;
}

animated => settings["animate_solving"];
(level[end::y][end::x])::marked => true;
loop {
    if (animated) {
        println(levelStr());
        time::halt(100);
    }
    (level[winner::y][winner::x])::marked => true;

    let brk => false;
    winner => match (winner::parent) {
        Some(val) -> val;
        None -> brk => true;
    };
    if (brk) break;
}

println("\n" * 100);
println(levelStr());
println("─" * sz + st + "─" * sz);
